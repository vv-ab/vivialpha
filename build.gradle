buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'ch.epfl.scala:gradle-bloop_2.13:1.5.3-28-373a64c9'
    }
}

apply plugin: "scala"
apply plugin: "bloop"
apply plugin: "distribution"

ext {
    additionalCompileOptions = [
        "-no-ident",
        "-feature",
        "-Ycheck-all-patmat",
    ]
}

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/vv-ab/compilersandbox")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GBR_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("GBR_KEY")
        }
    }
}

dependencies {
    implementation "org.scala-lang:scala3-library_3:3.2.0"
    implementation "vv-ab:compilersandbox:0.1.2"
    testImplementation "org.scalatest:scalatest_3:3.2.14"
    testImplementation "org.scalatestplus:junit-4-13_3:3.2.12.0"
}

compileScala {
    scalaCompileOptions.additionalParameters = additionalCompileOptions
}

compileTestScala {
    scalaCompileOptions.additionalParameters = additionalCompileOptions
}

jar {
    manifest.attributes([
        "Main-Class": 'vivialpha/App',
        "Class-Path": configurations.runtimeClasspath.collect { "libs/" + it.getName() }.join(' ')
    ])
}

distributions {
    main {
        contents {
            from(jar.outputs) {
                rename("${project.name}-${version}.jar", "${project.name}.jar")
            }
            from(configurations.runtimeClasspath) {
                into "libs"
            }
        }
    }
}
